plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'jacoco'
}

task generateXml(type:JavaExec) {
    main = 'com.hubsante.model.XmlGenerator'
    classpath = sourceSets.main.runtimeClasspath
}

group = 'com.hubsante'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.2'
    implementation 'javax.validation:validation-api:2.0.1.Final'
    implementation 'jakarta.annotation:jakarta.annotation-api:1.3.5'
    implementation 'com.networknt:json-schema-validator:1.0.87'
    implementation 'org.json:json:20240303'
    implementation 'org.projectlombok:lombok:1.18.22'
    implementation 'org.junit.jupiter:junit-jupiter-params:5.8.2'
    implementation 'com.google.guava:guava:32.1.3-jre'
    implementation 'ch.qos.logback:logback-classic:1.3.8'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    if (project.hasProperty('functional')) {
        exclude '**/*Technical*.class'
    } else if (project.hasProperty('technical')) {
        include '**/*Technical*.class'
    }
    finalizedBy jacocoTestReport
}

tasks.named('publish') {
    configure {
        dependsOn("test")
    }
}

license {
    header = file('LICENSE_HEADER.txt')
    strictCheck = true

    ext {
        company = "Agence du Numerique en Sante (ANS)"
        year = licenseYear()
    }

    mapping 'java', 'JAVADOC_STYLE'
    include "**/*.java"
    exclude "**/test/**"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ansforge/SAMU-Hub-Modeles'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

static def licenseYear() {
    def currentYear = new Date().format('yyyy')
    def licenseYear

    if (currentYear != "2023") {
        licenseYear = "2023-" + currentYear
    } else {
        licenseYear = "2023"

    }
    return licenseYear
}
