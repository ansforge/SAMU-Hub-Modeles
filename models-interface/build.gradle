plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.hierynomus.license' version '0.16.1'
    id 'jacoco'
}

group = 'com.hubsante'
version = '0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.15.2'
    implementation 'com.google.guava:guava:32.1.3-jre'
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
    }
}

allprojects {
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named('publish') {
    configure {
        dependsOn("test")
    }
}

license {
    header = file('../LICENSE_HEADER.txt')
    strictCheck = true

    ext {
        company = "Agence du Numerique en Sante (ANS)"
        year = licenseYear()
    }

    mapping 'java', 'JAVADOC_STYLE'
    include "**/*.java"
    exclude "**/test/**"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = 'https://maven.pkg.github.com/ansforge/SAMU-Hub-Modeles'
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

static def licenseYear() {
    def currentYear = new Date().format('yyyy')
    def licenseYear

    if (currentYear != "2023") {
        licenseYear = "2023-" + currentYear
    } else {
        licenseYear = "2023"

    }
    return licenseYear
}